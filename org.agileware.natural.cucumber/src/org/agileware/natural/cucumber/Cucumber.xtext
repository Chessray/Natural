grammar org.agileware.natural.cucumber.Cucumber hidden(WS, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate cucumber "http://www.agileware.org/natural/cucumber"

Feature:
	tags+=Tag*
	'Feature:'
	title=Title
	narrative+=Narrative*
	background=Background?
	scenarios+=(Scenario | ScenarioOutline)+;

Background:
	'Background:' EOL*
	title=Title?
	narrative+=Narrative*
	steps+=Step+;
	
Scenario:
	tags+=Tag*
	'Scenario:' EOL*
	title=Title?
	narrative+=Narrative*
	steps+=Step+;

ScenarioOutline:
	tags+=Tag*
	'Scenario Outline:' EOL*
	title=Title?
	narrative+=Narrative*
	steps+=Step+
	examples=Examples;
	
Step:
	STEP_KEYWORD
	description=StepDescription EOL*
	attach+=(Table | DocString)*;

Examples:
	'Examples:' EOL*
	title=Title?
	narrative+=Narrative*
	table=Table;
	
Table:
	rows+=TABLE_ROW+ EOL*;
	
DocString:
	content=DOC_STRING EOL*;

Title:
	(WORD | INT | STRING | PLACEHOLDER) (WORD | INT | STRING | PLACEHOLDER | STEP_KEYWORD | TAGNAME)* EOL+;

Narrative:
	(WORD | INT | STRING | PLACEHOLDER) (WORD | INT | STRING | PLACEHOLDER | STEP_KEYWORD | TAGNAME)* EOL+;

StepDescription:
	(WORD | INT | STRING | PLACEHOLDER | STEP_KEYWORD | TAGNAME)* EOL?;
	
Tag: name=TAGNAME EOL?;

terminal INT returns ecore::EDouble: '-'? ('0'..'9')+ ('.' ('0'..'9')+)?;

terminal STEP_KEYWORD: ('Given' | 'When' | 'Then' | 'And' | 'But');

terminal PLACEHOLDER: '<' !('>' | ' ' | '\t' | '\n' | '\r')+ '>';
	
terminal TABLE_ROW: '|' (!('|' | '\n' | '\r')* '|')+ EOL;

terminal DOC_STRING: ('"""' -> '"""' | "'''" -> "'''") EOL;

terminal STRING:
	'"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | '"' | '\r' | '\n'))* '"' |
	"'" ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | "'" | '\r' | '\n'))* "'";

terminal SL_COMMENT: '#' !('\n' | '\r')* EOL;

terminal TAGNAME: '@' !('@' | ' ' | '\t' | '\n' | '\r')+ ;

terminal WORD: !('@' | '|' | ' ' | '\t' | '\n' | '\r') !(' ' | '\t' | '\n' | '\r')*;

terminal WS: (' ' | '\t');

terminal EOL: ('\r'? '\n'?);