grammar org.rlogiacco.eclipse.jbehave.JBehaveDSL hidden(SPACE, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate jBehaveDSL "http://www.rlogiacco.org/eclipse/jbehave/JBehaveDSL"

Model:
	story=Story;

Story:
	'Story:' title=Line
	description+=Line*
	narrative=Narrative?
	scenarios+=Scenario+;

Narrative:
	'Narrative:' title=Line
	description+=Line*
	goal=InOrderTo
	role=AsA
	condition=IWantTo;

InOrderTo: 
	'In order to' body+=Line+;
	
AsA: 
	'As a' body+=Line+;

IWantTo: 
	'I want to' body+=Line+;
	
Scenario:
	'Scenario:' title=Line
	description+=Line*
	givenStories=GivenStories?
	steps+=Step+
	examples=Examples?;

GivenStories:
	'GivenStories:' paths+=StoryPath+;

StoryPath:
	(WORD ','?);

Step:
	type=('Given' | 'And' | 'When' | 'Then' | 'But')
	description=Line;

Examples:
	'Examples:' table=Table;
	
Table:
	header=TableRow
	rows+=TableRow+;
	
TableRow: 
	(EOC (WORD)+)+ EOC EOL;
 	
Line:
	(STRING | WORD | INT | ANY_OTHER)+ EOL*;

Tag:
	'@' WORD EOL;

terminal WORD:
	'^'? ('a'..'z' | 'A'..'Z' | '_' ) ('a'..'z' | 'A'..'Z' | '_' | '0'..'9' | "'" | "#" |
		('\\' ( 'u' | '"' | "'" | '\\'))
	)*;

terminal INT returns ecore::EInt:
	('0'..'9')+;

terminal STRING:
	'"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | '"'))* '"' |
	"'" ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | "'"))* "'";

terminal SL_COMMENT:
	'#' !('\n' | '\r')* ('\r'? '\n')?;

terminal SPACE:
	(' ' | '\t')+;

terminal EOL:
	('\r'? '\n')?;

terminal EOC:
	'|';

terminal ANY_OTHER:
	.;