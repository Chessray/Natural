grammar org.rlogiacco.eclipse.cucumber.CucumberDSL hidden(SPACE, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate cucumberDSL "http://www.rlogiacco.org/eclipse/cucumber/CucumberDSL"

Feature:
	tags+=Tag*
	'Feature:'
	description+=Description+
	background=Background?
	scenarios+=(Scenario | ScenarioOutline)+;

Background:
	'Background:' 
	description+=Description+
	steps+=Step+;
	
Scenario:
	tags+=Tag*
	'Scenario:'
	description+=Description+
	conditions+=Step+;

ScenarioOutline:
	tags+=Tag*
	'Scenario Outline:' 
	description+=Description+
	conditions+=Step+
	examples=Examples;
	
Step:
	description=StepDescription EOL*
	table=Table?;

Examples:
	'Examples:' 
	description+=Description*
	table=Table;
	
Table:
	rows+=TABLE_ROW+ EOL*;

Description:
	Content EOL+;

StepDescription:
	STEP_KEYWORD (STRING | WORD | INT | ANY | PLACEHOLDER)+;
	
Content:
	(STRING | WORD | INT | ANY | PLACEHOLDER) (AT | STRING | WORD | INT | ANY | PLACEHOLDER | STEP_KEYWORD)*;

Tag: AT name=WORD EOL?;


terminal STEP_KEYWORD: ('Given' | 'When' | 'Then' | 'And' | 'But');

terminal PLACEHOLDER: '<' ('a'..'z' | 'A'..'Z') !('>' | '\n' | '\r')* '>';
	
terminal TABLE_ROW: '|' (!('|' | '\n' | '\r')* '|')+ SPACE* EOL;

terminal WORD: ('a'..'z' | 'A'..'Z') !(SPACE | '\n' | '\r')*;

terminal INT returns ecore::EDouble: '-'? ('0'..'9')+ ('.' ('0'..'9')+)?;

terminal STRING:
	'"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | '"'))* '"' |
	"'" ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | "'"))* "'";

terminal SL_COMMENT: '#' !('\n' | '\r')* EOL;

terminal SPACE: (' ' | '\t');

terminal EOL: '\r'? '\n'?;

terminal AT: '@';

terminal ANY: .;