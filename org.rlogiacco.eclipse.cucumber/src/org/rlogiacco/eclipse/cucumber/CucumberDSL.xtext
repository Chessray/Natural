grammar org.rlogiacco.eclipse.cucumber.CucumberDSL hidden(SPACE, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate cucumberDSL "http://www.rlogiacco.org/eclipse/cucumber/CucumberDSL"

Model:
	feature=Feature;

Feature:
	EOL*
	tags+=Tag*
	'Feature:' title=Line EOL*
	description+=Line* EOL*
	background=Background? EOL*
	scenarios+=(Scenario | ScenarioOutline)+ EOL*;

Background:
	'Background:' title=Line? EOL*
	steps+=Step+ EOL*;
	
Scenario:
	tags+=Tag*
	'Scenario:' title=Line EOL*
	description+=Line* EOL*
	conditions+=Step+ EOL*;

ScenarioOutline:
	tags+=Tag*
	'Scenario Outline:' title=Line EOL*
	description+=Line* EOL*
	conditions+=Step+ EOL*
	examples=Examples EOL*;
	
Step:
	type=('Given' | 'When' | 'Then' | 'And' | 'But') EOL* 
	description=Line EOL*
	table=Table? EOL*;

Examples:
	'Examples:' title=Line EOL*
	table=Table EOL*;
	
Line:
	content=CharSequence EOL;

Table:
	rows+=Row+;

Row:
	'|' cells+=Cell+ EOL;

Cell:
	content=CharSequence '|';	
Tag:
	'@' WORD EOL?;

CharSequence:
	(STRING | WORD | INT | ANY_OTHER)+;

terminal WORD:
	'^'? ('a'..'z' | 'A'..'Z' | '_' ) ('a'..'z' | 'A'..'Z' | '_' | '0'..'9' | "'" | "#" |
		('\\' ( 'u' | '"' | "'" | '\\'))
	)*;

terminal INT returns ecore::EInt:
	('0'..'9')+;

terminal STRING:
	'"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | '"'))* '"' |
	"'" ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | "'"))* "'";
	
terminal SL_COMMENT:
	'#' !('\n' | '\r')* EOL;

terminal SPACE:
	(' ' | '\t')+;

terminal EOL:
	('\r'? '\n') | EOF;

terminal ANY_OTHER:
	.;